{
  package main
}

Start 
  <- _ (
       ins:Instruction Comment?      { return Forward(ins) }
     / lbl:Label                     { return Forward(lbl) }
     / comment:Comment               { return Forward(comment) }
   ) !.

Instruction 
  <- _ set:'!'? _ "add" cnd:Condition? _ rd:Register _ ra:Register _ rb:Register _  { return NewRegInstr(set, "add", cnd, rd, ra, rb) }
   / _ set:'!'? _ "add" cnd:Condition? _ rd:Register _ ra:Register _ num:Number _   { return NewImm12Instr(set, "add", cnd, rd, ra, num) }
   / _ set:'!'? _ "add" cnd:Condition? _ rd:Register _ rb:Register _                { return NewRegInstr(set, "add", cnd, rd, rd, rb) }
   / _ set:'!'? _ "add" cnd:Condition? _ rd:Register _ num:Number _                 { return NewImm16Instr(set, "add", cnd, rd, num) }
  
   / cmd:Command _
     rd:Register _ 
     ra:Register _ 
     ('[' _)? 
     rb:Register _ 
     (']' _)?                         { return NewRegInstruction(cmd.(*Command), rd.(*Register), ra.(*Register), rb.(*Register)) }
   
   / cmd:Command _
     rd:Register _ 
     ra:Register _ 
     ('[' _)? 
     num:Number _ 
     (']' _)?                         { return NewImmInstruction(cmd.(*Command), rd.(*Register), ra.(*Register), num.(*Number)) }

   / cmd:Command _ 
     lbl:Label _                      { return NewBraInstruction(cmd.(*Command), lbl.(*Label)) }

Command 
  <- set:'!'? _
     cmd:Mnemonic 
     cnd:Condition?                   { return NewCommand(set, cmd, cnd) }

Mnemonic <- [a-z][a-z][a-z]           { return NewString(c.text) }

Condition <- [a-z][a-z]               { return NewString(c.text) }

Register <- ("sp" / "rp" / "ip" / 'r' [0-9]+)   { return NewRegister(c.text) }

Number <- num:(HexNumber / DecNumber) { return Forward(num) } 

DecNumber <- '-'? [0-9]+              { return NewNumber(c.text, 10) }

HexNumber <- "0x" [0-9a-f]+           { return NewNumber(c.text, 16) }

Label <- '@' [a-zA-Z0-9]+             { return NewLabel(c.text) }

Comment <- "//" [^\n\r]*              { return NewComment() }

_ "whitespace" <- [ \n\r\t]*
